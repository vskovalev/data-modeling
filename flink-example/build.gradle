/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

plugins {
  id "java"
  id "application"
  id "com.github.johnrengelman.shadow" version "7.1.2"
}

group "io.iceberg.flink.lor.example"
version "0.0.1"
mainClassName = 'io.iceberg.flink.lor.example.LORSink'
description = "Streaming randomly generated Lord of the Rings data to a Tabular table using Flink"

compileJava {
  options.release.set(11)
}

repositories {
  mavenCentral()
  maven {
    url "https://tabular-repository-public.s3.amazonaws.com/releases"
  }
  maven {
    url "https://repository.apache.org/content/repositories/snapshots"
  }
}

ext {
  flinkVersion = "1.16.1"
  flinkShortVersion = "1.16"
  scalaVersion = "2.12"
  icebergVersion = "1.2.1"
  tabularVersion = "1.4.3"
  hadoopVersion = "3.3.2"
}

configurations {
  flinkShadowJar // dependencies to include in the shadow jar

  // provided by Flink...
  flinkShadowJar.exclude group: 'org.apache.flink', module: 'force-shading'
  flinkShadowJar.exclude group: 'com.google.code.findbugs', module: 'jsr305'
  flinkShadowJar.exclude group: 'org.slf4j'
  flinkShadowJar.exclude group: 'org.apache.logging.log4j'
}

dependencies {
  flinkShadowJar "org.apache.iceberg:iceberg-flink-runtime-${flinkShortVersion}:${icebergVersion}"
  flinkShadowJar "org.apache.hadoop:hadoop-common:${hadoopVersion}" // needed by Iceberg Flink
  flinkShadowJar "io.tabular:tabular-client:${tabularVersion}"
  flinkShadowJar group: 'com.github.javafaker', name: 'javafaker', version: '1.0.2'

  // these are provided by Flink...
  implementation "org.apache.flink:flink-streaming-java:${flinkVersion}"
  implementation "org.apache.flink:flink-clients:${flinkVersion}"
  implementation "org.apache.flink:flink-table-runtime:${flinkVersion}"
  implementation "org.apache.iceberg:iceberg-flink-runtime-${flinkShortVersion}:${icebergVersion}"
  implementation "org.apache.hadoop:hadoop-common:${hadoopVersion}" // needed by Iceberg Flink
  implementation "io.tabular:tabular-client:${tabularVersion}"
  implementation group: 'com.github.javafaker', name: 'javafaker', version: '1.0.2'
}

sourceSets {
  main.compileClasspath += configurations.flinkShadowJar
  main.runtimeClasspath += configurations.flinkShadowJar

  test.compileClasspath += configurations.flinkShadowJar
  test.runtimeClasspath += configurations.flinkShadowJar

  javadoc.classpath += configurations.flinkShadowJar
}

shadowJar {
  zip64 true
  configurations = [project.configurations.flinkShadowJar]
}
